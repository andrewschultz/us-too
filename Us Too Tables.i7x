Version 1/241005 of Us Too Tables by Andrew Schultz begins here.

"This lays out the major tables for Us Too for easy indexing and searching, as well as any rules related to the tables."

[REMINDER: mrc.py to create new point scoring action]

volume the main table

[note: # of rows != total points since we start with HOW SO]

table of main oronyms
w1 (text)	w2 (text)	first-hom (text)	second-hom	hom-txt-rule (rule)	first-exact	first-close	second-exact	second-close	part-explain	think-cue	okflip	core	idid	everfail	best-room	check-rule	run-rule	wfull (topic)	think-advice (text)
"loose"	"intro"	--	--	--	false	false	false	false	"finding something hidden in the lucent row"	false	true	false	false	false	my new mine ooh	pre-loose-intro rule	post-loose-intro rule	--	--
"try"	"quest"	--	--	--	false	false	false	false	"how to get started and what to do"	false	true	true	false	false	mine ooh	pre-try-quest rule	post-try-quest rule	--	--
"be"	"strong"	--	--	--	false	false	false	false	"getting rid of the beast"	false	true	true	false	false	mine ooh	pre-be-strong rule	post-be-strong rule	--	--
"nice"	"warm"	--	--	--	false	false	false	false	"making the swarm less aggressive"	false	true	true	false	false	mine ooh	pre-nice-warm rule	post-nice-warm rule	--	--
"mess"	"pot"	--	--	--	false	false	false	false	"making the spot into something more useful"	false	true	true	false	false	mine ooh	pre-mess-pot rule	post-mess-pot rule	--	--
"meh"	"skit"	--	--	--	false	false	false	false	"having harmless, useless fun with the mess kit"	false	true	false	false	false	mine ooh	pre-meh-skit rule	post-meh-skit rule	--	--
"a"	"stew"	--	--	--	false	false	false	false	"achieving the game's objective"	false	true	true	false	false	mine ooh	pre-a-stew rule	post-a-stew rule	--	"You can make [b]A STEW[r] [once-now of pre-a-stew rule] you have all the ingredients you need."
"herb"	"ranch"	--	--	--	false	false	false	false	"finding the first place to visit from the Ur-Branch"	false	true	true	false	false	ur branch	pre-herb-ranch rule	post-herb-ranch rule	--	--
"summer"	"bay"	--	--	--	false	false	false	false	"letting the herb reveal a new location"	false	true	true	false	false	ur branch	pre-summer-bay rule	post-summer-bay rule	--	--
"blah"	"copse"	--	--	--	false	false	false	false	"avoiding the black ops"	false	true	true	false	false	ur branch	pre-blah-copse rule	post-blah-copse rule	--	--
"guard"	"entry"	--	--	--	false	false	false	false	"finding a way past the garden tree"	false	true	true	false	false	ur branch	pre-guard-entry rule	post-guard-entry rule	--	--
"be"	"chill"	--	--	--	false	false	false	false	"dealing with the beach, ill"	false	true	true	false	false	Beach Ill	pre-be-chill rule	post-be-chill rule	--	--
"punt"	"weaker"	--	--	--	false	false	false	false	"trading with the pun tweaker"	false	true	true	false	false	Beach Ill	pre-punt-weaker rule	post-punt-weaker rule	--	--
"base"	"pikes"	--	--	--	false	false	false	false	"figuring a way through the bay spikes"	false	true	true	false	false	a pile up isle	pre-base-pikes rule	post-base-pikes rule	--	--
"pie"	"crust"	"pi"	--	--	false	false	false	false	"finding what's under the pike rust"	false	true	true	false	false	A Pile Up Isle	pre-pie-crust rule	post-pie-crust rule	--	--
"hike"	"up"	--	--	--	false	false	false	false	"reaching the high cup"	false	true	true	false	false	a pile up isle	pre-hike-up rule	post-hike-up rule	--	--
"sword"	"ark"	"soared"	--	--	false	false	false	false	"finding something in the Sore Dark"	false	true	true	false	false	sore dark	pre-sword-ark rule	post-sword-ark rule	--	--
"blast"	"ring"	--	--	--	false	false	false	false	"finding the blah string's true purpose"	false	true	true	false	false	sore dark	pre-blast-ring rule	post-blast-ring rule	--	--
"terrible"	"aid"	--	--	--	false	false	false	false	"becoming worthy of the Terra Blade"	false	true	true	false	false	sore dark	pre-terrible-aid rule	post-terrible-aid rule	--	--
"board"	"red"	"bored"	--	--	false	false	false	false	"finding something in the Bore Dread"	false	true	true	false	false	bore dread	pre-board-red rule	post-board-red rule	--	--
"nah"	"queue"	--	--	--	false	false	false	false	"repelling the [team]"	false	true	true	false	false	blah copse	pre-nah-queue rule	post-nah-queue rule	--	--
"surf"	"ready"	--	--	--	false	false	false	false	"giving Sir Freddie a new direction"	false	true	true	false	false	blah copse	pre-surf-ready rule	post-surf-ready rule	--	--
"or"	"clerk"	--	--	--	false	false	false	false	"figuring what Sir Freddie was scared of"	false	true	true	false	false	blah copse	pre-or-clerk rule	post-or-clerk rule	--	--
"more"	"flares"	--	--	--	false	false	false	false	"activating the morph lairs"	false	true	true	false	false	morph lairs	pre-more-flares rule	post-more-flares rule	--	--
"probe"	"all"	--	--	--	false	false	false	false	"finding a purpose for the pro ball"	false	true	true	false	false	ur branch	pre-probe-all rule	post-probe-all rule	--	--
"peace"	"talks"	--	--	--	false	false	false	false	"finding something incorporeal within the Pea Stalks"	false	true	true	false	false	pea stalks	pre-peace-talks rule	post-peace-talks rule	--	--
"pea"	"pod"	--	--	--	false	false	false	false	"finding something unusual within the Pea Stalks"	false	true	false	false	false	pea stalks	pre-pea-pod rule	post-pea-pod rule	--	--
"can"	"take"	--	--	--	false	false	false	false	"dispelling the Can't-Ache"	false	true	true	false	false	dome aching	pre-can-take rule	post-can-take rule	--	--
"dough"	"making"	--	--	--	false	false	false	false	"doing something positive in [dome]"	false	true	true	false	false	dome aching	pre-dough-making rule	post-dough-making rule	--	--
"gray|grey"	"tin|tins"	--	--	--	false	false	false	false	"discover what the Great Inns hold"	false	true	true	false	false	dome aching	pre-gray-tins rule	post-gray-tins rule	--	--
"in"	"earnest"	--	--	--	false	false	false	false	"figure how to visit the inner nest"	false	true	true	false	false	dome aching	pre-in-earnest rule	post-in-earnest rule	--	--
"pry"	"more"	--	--	--	false	false	false	false	"figuring how to take the prime oar"	false	true	true	false	false	inner nest	pre-pry-more rule	post-pry-more rule	--	--
"oh"	"clever"	--	--	--	false	false	false	false	"operating the oak lever"	false	true	true	false	false	fort earns four turns	pre-oh-clever rule	post-oh-clever rule	--	--
"bold"	"itch"	--	--	--	false	false	false	false	"figuring how to get back on your feet in Bowl/Ditch"	false	true	true	false	false	bowl ditch	pre-bold-itch rule	post-bold-itch rule	--	--
"claim"	"it"	--	--	--	false	false	false	false	"gathering the clay mitt"	false	true	true	false	false	Bowl Ditch	pre-claim-it rule	post-claim-it rule	--	--
"stark"	"raft"	--	--	--	false	false	false	false	"finding more appropriate transport than the star craft"	false	true	true	false	false	turbo tours	pre-stark-raft rule	post-stark-raft rule	--	--
"tour"	"boaters"	--	--	--	false	false	false	false	"bringing more business to Turbo Tours"	false	true	false	false	false	turbo tours	pre-tour-boaters rule	post-tour-boaters rule	--	--
"find"	"rinks"	--	--	--	false	false	false	false	"seeing another place on the Farm Isles"	false	true	true	false	false	far miles farm isles	pre-find-rinks rule	post-find-rinks rule	--	--
"barn"	"open"	--	--	--	false	false	false	false	"overcoming rejection from the bar"	false	true	true	false	false	far miles farm isles	pre-barn-open rule	post-barn-open rule	--	--
"saw"	"bakers"	--	--	--	false	false	false	false	"finding signs of life in Sob Acres"	false	true	true	false	false	sob acres	pre-saw-bakers rule	post-saw-bakers rule	--	--
"dell"	"eastern"	--	--	--	false	false	false	false	"making Sob Acres less forbidding"	false	true	true	false	false	deli stern	pre-dell-eastern rule	post-dell-eastern rule	--	--
"fell"	"trap"	--	--	--	false	false	false	false	"revealing what's behind the felt wrap"	false	true	true	false	false	fort earns	pre-fell-trap rule	post-fell-trap rule	--	--
"cellar"	"bin"	--	--	--	false	false	false	false	"escaping the Cell, Urban"	false	true	true	false	false	cell urban	pre-cellar-bin rule	post-cellar-bin rule	--	--
"too"	"broad"	--	--	--	false	false	false	false	"how to fracture the tube/rod"	false	true	true	false	false	tube rod	pre-too-broad rule	post-too-broad rule	--	--
"ho"	"languor|langour"	--	--	--	false	false	false	false	"dealing with the paralyzing whole anger"	false	true	true	false	false	tude ark	pre-ho-langour rule	post-ho-langour rule	--	--
"say"	"jokes"	--	--	--	false	false	false	false	"get by the Sage Oaks"	false	true	true	false	false	sage oaks	pre-say-jokes rule	post-say-jokes rule	--	--
"gas"	"pouch"	--	--	--	false	false	false	false	"see how to stop gasping OUCH"	false	true	true	false	false	sage oaks	pre-gas-pouch rule	post-gas-pouch rule	--	--
"damp"	"ink"	--	--	--	false	false	false	false	"clue for guessing one word right"	false	true	true	false	false	dam pink	pre-damp-ink rule	post-damp-ink rule	--	--
"scry"	"broom"	--	--	--	false	false	false	false	"clue for guessing one word right"	false	true	true	false	false	scribe room	pre-scry-broom rule	post-scry-broom rule	--	--
"pro"	"sweeping"	--	--	--	false	false	false	false	"clue for guessing one word right"	false	true	true	false	false	dust which	pre-pro-sweeping rule	post-pro-sweeping rule	--	--
"summon"	"cheese"	--	--	--	false	false	false	false	"figuring what some munchies are"	false	true	true	false	false	--	pre-summon-cheese rule	post-summon-cheese rule	--	--
"den"	"specs"	--	--	--	false	false	false	false	"finding something more useful than dense pecs"	false	true	true	false	false	--	pre-den-specs rule	post-den-specs rule	--	--

chapter mine ooh scoring

a wordtwisting rule (this is the pre-try-quest rule):
	if sco-try-quest is true:
		vcal "You already have an idea of your quest!";
		already-done;
	ready;

this is the post-try-quest rule:
	now sco-try-quest is true;
	say "Yes, yes. That's what really matters. Your quest.[paragraph break]You doze off, and when you awake, you find ... well, a beast, wrong, blocking your way.";
	move beast wrong to mine ooh;
	now player has aight;
	declue the player;

a wordtwisting rule (this is the pre-nice-warm rule):
	if nigh swarm is not touchable, unavailable;
	ready;

this is the post-nice-warm rule:
	now sco-nice-warm is true;
	say "The nigh swarm grows less volatile and, yes, more nice, warm. It rises in the air, then buzzes off to somewhere to maybe be nice, cool, too. Perhaps there is a nigh school![paragraph break]Behind the nigh swarm is a pro ball.";
	moot nigh swarm;
	move pro ball to beach ill;

a wordtwisting rule (this is the pre-be-strong rule):
	if player is not in mine ooh, unavailable;
	if sco-be-strong is true:
		vcal "You already felt strong! There's the matter of details, but those aren't nailed down by repetition here.";
		already-done;
	ready;

this is the post-be-strong rule:
	now sco-be-strong is true;
	say "You manage to stand up to the beast a bit. It could beat you, sure, but you're not worth the effort. It trudges off, no longer blocking you from leaving. It has easier prey. 'Be stranger, beast-ranger,' you call out, gaining confidence.";
	moot beast wrong;

a wordtwisting rule (this is the pre-mess-pot rule):
	if meh spot is not touchable and mess pot is not touchable, unavailable;
	if sco-mess-pot is true:
		vcal "You already found the mess pot!";
		already-done;
	ready;

this is the post-mess-pot rule:
	now sco-mess-pot is true;
	say "Ah. There it is. You, in fact, find a whole mess kit."; [??mess kit as synonym]
	move mess pot to mine ooh;
	moot meh spot;

a wordtwisting rule (this is the pre-meh-skit rule):
	if mess pot is not touchable, unavailable;
	if sco-meh-skit is true:
		vcal "The skit would go from meh to cringe if you did it again.";
		already-done;
	ready;

this is the post-meh-skit rule:
	now sco-meh-skit is true;
	say "Perhaps it's too obvious, but you decide to have fun, and you do.";
	declue mess pot;

a wordtwisting rule (this is the pre-a-stew rule):
	if player is not in mine ooh:
		vcp "You need to be back in your mine for this.";
		not-yet;
	if sco-a-stew is false and debug-win is false:
		vcp "You don't have all the stew ingredients you need!";
		not-yet;
	ready;

this is the post-a-stew rule:
	now sco-a-stew is true;
	say "Well, that does it! You make the stew, and you win. As you make the stew, you reflect it is not MY stew, which would be kind of a me-ill meal, but A stew.[paragraph break]You make other random seeming stews over there years, but none quite has the flavor of this one.";
	end the story finally saying "A swell ace! WELL!";
	follow the shutdown rules;

a wordtwisting rule (this is the pre-loose-intro rule):
	if player is not in my new mine ooh, unavailable;
	if lucent row is not in my new mine ooh, unavailable;
	ready;

this is the post-loose-intro rule:
	now sco-loose-intro is true;
	say "A loose intro flutters down from the lucent row. You pick it up. As you do, the lucent row stops glowing and becomes a ledge. A Goal-Edge-Go Ledge, to be precise.";
	now player has loose intro;
	moot lucent row;
	move ledge to My New Mine Ooh;

book room-variant scoring

a wordtwisting rule (this is the pre-den-specs rule):
	if player does not have dense pecs, unavailable;
	ready;

this is the post-den-specs rule:
	now sco-den-specs is true;
	say "You lose strength, strength you didn't really need, and wind up with a pair of glasses that may help you find a secret passage somewhere. You hope.";
	now player has den specs;
	moot dense pecs;

a wordtwisting rule (this is the pre-summon-cheese rule):
	if aight is not examined, unavailable;
	if player does not have aight, unavailable;
	if sco-summon-cheese is true:
		vcal "You already got some munchies! No need to be greedy";
		already-done;
	ready;

this is the post-summon-cheese rule:
	now sco-summon-cheese is true;
	say "You're confident you've figured what the munchies are. And you know what? You're right! You have all sorts of cheese now.";
	now player has cheese;

chapter ur branch scoring

a wordtwisting rule (this is the pre-herb-ranch rule):
	if player is not in ur branch, unavailable;
	if sco-herb-ranch is true:
		vcal "You already found the herb ranch! One free sample should be enough.";
		already-done;
	ready;

this is the post-herb-ranch rule:
	now sco-herb-ranch is true;
	say "You find a path to a herb ranch. Of course every stew needs herbs. The farmer there gives you a sample saying 'Some herb, eh?' The phrase sticks with you as conversation continues.[paragraph break]It's a surprisingly enlightening one. What can you do with it?";
	now player has herbs;
	declue-here;

a wordtwisting rule (this is the pre-probe-all rule):
	if pro ball is not touchable, unavailable;
	if sco-probe-all is true:
		vcal "You already probed the pro ball enough to get a feel for hidden exits in your surroundings! More will appear as you need them.";
		already-done;
	ready;

this is the post-probe-all rule:
	now sco-probe-all is true;
	say "You reach out to the pro ball. It seems to sense what you want it for. You take it, then look at it from many different angles. Reflected in it, you see ... well, a meh spot [here-in of my new mine ooh] that could use sprucing up. But how?[paragraph break]Yet much of the pro ball remains cloudy. Perhaps it will reveal what you need, as you need it.";
	say "It also reveals something near here. You notice some sort of ship. You wave, and its captain, so to speak, waves back. They introduce themselves as the Pun Tweaker. Their ship is the Gauche Hip Go Ship. They immediately start in with some crunching puns.[paragraph break][i][bracket][b]NOTE[r][i]: you don't need to examine the pro ball from here on out for it to open new passages.[close bracket][i][line break]";
	move meh spot to my new mine ooh;
	now player has pro ball;
	move pun tweaker to beach ill;
	move ship to beach ill;
	declue pro ball;
	[some passages you hadn't even considered away from the Ur-Branch. You lose yourself in it for a while, and then, when you look up, you see ]

a wordtwisting rule (this is the pre-summer-bay rule):
	if player does not have herbs, unavailable;
	if sco-summer-bay is true:
		vcal "You already used the herb to expand your mind! It is only good for seasoning now";
		already-done;
	if player is not in ur branch:
		vcp "Perhaps you should use the herb in a more wide-open place. Like the Ur-Branch where you got it.";
		not-yet;
	ready;

this is the post-summer-bay rule:
	now sco-summer-bay is true;
	say "The herb gives off a weird ... well, not quite a smell. But it opens your mind to new passages, passages that might actually lead somewhere, without having to light it or do anything adults who drank a lot told you was very, very dangerous.[paragraph break]Your newly opened mind discerns a safe passage east. Hey, that's a start!";
	reveal Beach Ill to east;
	declue herbs;

a wordtwisting rule (this is the pre-guard-entry rule):
	if player is not in ur branch, unavailable;
	if garden tree is not in ur branch, unavailable;
	if sco-guard-entry is true:
		vcal "You already snuck past the garden tree so you could go [b]IN[r]!";
		already-done;
	ready;

this is the post-guard-entry rule:
	now sco-guard-entry is true;
	say "You look for a way past the tree, and suddenly you have an idea, and once you know what to look for, it's easy. You're less threatening than [the forest team], so it's no problem. The protective magic or whatever is fooled, or maybe it thinks that if you're willing to risk the guard entry, you're not visiting for greedy purposes. Whichever it is, you make it...";
	move player to Pea Stalks;

a wordtwisting rule (this is the pre-blah-copse rule):
	if player is not in ur branch, unavailable;
	if north is branchcant, unavailable;
	if sco-blah-copse is true:
		vcp "You already figured how to evade the black ops! You can just go north if you need to.";
		not-yet;
	ready;

this is the post-blah-copse rule:
	now sco-blah-copse is true;
	say "Aha! That was what you were doing wrong. You just needed to tweak a thing or two as you walked north. This time, you avoid the black-ops site. Once you see the path, well, it's hard to un-see.";
	moot black ops;
	move player to Blah Copse;

book east branch scoring

chapter Beach Ill scoring

a wordtwisting rule (this is the pre-be-chill rule):
	if player is not in Beach Ill, unavailable;
	if sco-be-chill is true:
		vcal "The beach is already chill enough!";
		already-done;
	ready;

this is the post-be-chill rule:
	now sco-be-chill is true;
	move nigh swarm to Beach Ill;
	say "Your general sense of malaise disperses, which makes you more observant. Observant enough to notice a nigh swarm which ... keeps things from being perfect.";
	declue-here;

a wordtwisting rule (this is the pre-pie-crust rule):
	if player is not in Beach Ill and pike rust is not in location of player, unavailable;
	if sco-pie-crust is true:
		vcal "You already de-rusted the pike!";
		already-done;
	ready;

this is the post-pie-crust rule:
	now sco-pie-crust is true;
	say "Yup! Pie crust!";
	now player has pie crust;

a wordtwisting rule (this is the pre-punt-weaker rule):
	if player is not in beach ill, unavailable;
	if sir freddie is not in beach ill and sir freddie is not moot:
		vcp "The pun tweaker says 'Yeah, maybe I have one. But what can you do for me?'";
		not-yet;
	if sco-peace-talks is false:
		vcp "Perhaps if you managed to get rid of Sir Freddie...";
		not-yet;
	if sco-punt-weaker is true:
		vcal "You already got the punt, weaker!";
		already-done;
	ready;

this is the post-punt-weaker rule:
	now sco-punt-weaker is true;
	say "'Ah, yes, that's it. A small gift for a small favor.' The pun tweaker goes to the back of [the ship], and you hear a splash. The pun tweaker reappers, clothes wet, pushing a pretty scrawny punt. 'It won't get very far, but there's something to the east. Oh, and I hope you have a way to steer it, too. Anyway, here you go. My repairs are complete!'";
	move punt weaker to beach ill;
	moot pun tweaker;
	moot ship;
	check-oar-punt;
	reveal Pile Up Isle to east;

to check-oar-punt:
	if player has prime oar and sco-punt-weaker is true:
		now prime oar is scenery;
		now prime oar is in location of player;
		say "You hook the prime oar up to the punt.";

book northwest branch scoring

chapter sore dark scoring

a wordtwisting rule (this is the pre-sword-ark rule):
	if sco-sword-ark is true:
		vcal "You don't need another sword! You just want to get out of here.";
		already-done;
	ready;

this is the post-sword-ark rule:
	now sco-sword-ark is true;
	say "It takes a while of feeling around, but your blah string does the job. You find a sword ark. In that sword ark is contained, shockingly, a sword! And not just any sword, but the TERRA BLADE. However, the blade is still out of reach. It's a pretty tall ark.";
	move sword ark to sore dark;
	move terra blade to sore dark;

a wordtwisting rule (this is the pre-blast-ring rule):
	if player does not have blah string, unavailable;
	if player is not in sore dark or sword ark is not in sore dark:
		vcal "You can see nothing to blast!";
		not-yet;
	ready;

this is the post-blast-ring rule:
	now sco-blast-ring is true;
	say "You aim the blah string at the sword ark. You feel a pull--yes, this must be when to transform the blah string. And boom! The sword ark shatters, and the Terra Blade falls to the ground.";
	moot blah string;
	moot sword ark;

a wordtwisting rule (this is the pre-terrible-aid rule):
	if player is not in sore dark and player does not have terra blade, unavailable;
	if sco-terrible-aid is true:
		vcal "You probably shouldn't actively diss the Terra Blade, now you're carrying it.";
		already-done;
	if sco-blast-ring is false:
		vcp "Hmm, yes, it doesn't matter if it's terrible while it's unattainable.";
		not-yet;
	ready;

this is the post-terrible-aid rule:
	now sco-terrible-aid is true;
	say "You make the point that the Terra Blade would be a terrible aid for your humble goals, whatever they may be. That proves you are worthy![paragraph break]You hear rumbling. You can't imagine there are any bigger secret here, so it seems like it's time to leave.";
	declue terra blade;
	block-and-back;

book northeast branch scoring

chapter bore dread scoring

a wordtwisting rule (this is the pre-board-red rule):
	if player is not in bore dread, unavailable;
	if sco-board-red is true:
		vcal "You shouldn't need another board!";
		already-done;
	ready;

this is the post-board-red rule:
	now sco-board-red is true;
	say "You turn up a board (red) ... it's narrow and six feet long, made of styrofoam, with CATCH THE WAVES and STAND ON THIS SIDE written on one side. Since it wasn't hard to find, you don't feel excited you found it, but you carry it anyway.";
	now player has board red;
	block-and-back;

book north branch scoring

chapter blah copse scoring

a wordtwisting rule (this is the pre-nah-queue rule):
	if forest is not in location of player, unavailable;
	if sco-nah-queue is true:
		vcal "You already brushed back [the forest] that way!";
		already-done;
	ready;

this is the post-nah-queue rule:
	now sco-nah-queue is true;
	say "You reject [the forest]'s jibes consistently and forcefully, not worrying about intellectual rigor. They give up on you. Someone else appears, relieved you got rid of them. He introduces himself as Sir Freddie. He thinks perhaps they are right, and he is not cut out for quests.";
	say "[line break][i][bracket][b]NOTE[r][i]: if you wish, you can refer to him as F or Sir F.[close bracket]";
	moot forest;
	move Sir Freddie to Blah Copse;

a wordtwisting rule (this is the pre-surf-ready rule):
	if Sir Freddie is not in location of player, unavailable;
	if sco-board-red is false:
		vcp "Sir Freddie finds that interesting, but ... he doesn't have any way to get started, and neither do you.";
		not-yet;
	if sco-surf-ready is true:
		vcal "But Sir Freddie has already found a new passion!";
		already-done;
	ready;

this is the post-surf-ready rule:
	now sco-surf-ready is true;
	say "Sir Freddie agrees with you, but he doesn't know how to get started. You have just the thing! Your red board is kind of bulky but not heavy. It'd be nice not to have to carry it around. He waves. 'See you later. Oh, one other thing... I think I heard an orc lurk to the north. Another reason to give up this questing.'";
	moot board red;
	move Sir Freddie to Beach Ill;

a wordtwisting rule (this is the pre-or-clerk rule):
	if player is not in blah copse, unavailable;
	if sco-surf-ready is false, unavailable;
	if sco-or-clerk is true:
		vcal "You already figured nothing awful was lurking to the north";
		already-done;
	ready;

this is the post-or-clerk rule:
	now sco-or-clerk is true;
	say "Haha! Sir Freddie seemed nice enough, but you guess he was scared of someone else who was scared and putting up a front. Sure enough, a clerk Sir Freddie could've beaten up with one hand (if Sir Freddie had the courage to fight, of course) comes out of a bunker with their hands up. They begin to apologize, but you say it's all right.[paragraph break]'You ... you aren't part of the forest team?' Certainly not, you assure them.[paragraph break]'That's a relief! I wish I had more to give you than this blah string I found. I have no clue what to do with it. Oh, and I should never have meddled with what's to the north ... but maybe you can ...'[paragraph break]With that, the clerk runs away, turning briefly to wave at you.";
	now player has blah string;
	reveal Morph Lairs to north;

chapter morph lairs scoring

a wordtwisting rule (this is the pre-more-flares rule):
	if player is not in morph lairs, unavailable;
	ready;

this is the post-more-flares rule:
	now sco-more-flares is true;
	say "You set off the flares, and what do you know? You see another flare coming towards the morph lairs from a distance. Fortunately you already started running to the Ur-Branch. Whereupon you see smoke you follow ... a bit. The path opens up a bit further to the southeast. You have somewhere new to go, though you can't exactly go back north.";
	now Dome Aching is mapped southeast of Ur Branch;
	now Ur Branch is mapped northwest of Dome Aching;
	wfak;
	now north is branchdone;
	move player to Ur Branch;

book inside scoring

chapter pea stalks scoring

a wordtwisting rule (this is the pre-peace-talks rule):
	if player is not in pea stalks, unavailable;
	if sco-peace-talks is true:
		vcal "You already fathomed peace talks!";
		already-done;
	ready;

this is the post-peace-talks rule:
	now sco-peace-talks is true;
	say "Getting back to nature and stuff leaves you at peace with yourself. So at peace, you want to spread that peace to others, whether or not they fully deserve it. However, now you're so at peace, you recognize you are disturbing the plants' peace. You retreat to the Ur-Branch.";
	if sco-pea-pod is false, now eyes-number of pea stalks is -43;
	inside-block-back;

a wordtwisting rule (this is the pre-pea-pod rule):
	if player is not in pea stalks, unavailable;
	if sco-pea-pod is true:
		vcal "There are no other odd pea pods.";
		already-done;
	ready;

this is the post-pea-pod rule:
	now sco-pea-pod is true;
	say "Oh wait! There is something that will add a bit of flavor! Actually, it's a weird giant pea pod!";
	now player has pea pod;
	inside-block-back;

to inside-block-back:
	if sco-pea-pod is true and sco-peace-talks is true:
		say "You feel a moment of zen. You've done what you could here, and you sense you don't fully belong. It's time to move on.";
		block-and-back;
	else if sco-pea-pod is true:
		say "You're pleased you found something out of the way, but perhaps there's something bigger to figure here.";
	else:
		say "In the big picture, it's good to understand peace talks. But maybe there's something fun and obscure to note before moving on.";

chapter A Pile Up Isle scoring

a wordtwisting rule (this is the pre-base-pikes rule):
	if player is not in a pile up isle, unavailable;
	if sco-base-pikes is true:
		vcal "You already cleared a path through the bay spikes!";
		already-done;
	ready;

this is the post-base-pikes rule:
	now sco-base-pikes is true;
	say "Well, someone must've been able to get through the bay spikes to dump stuff on the pile, you reason. And you discover something -- base pikes, one you wouldn't want to use on any person, but then again, these spikes could hurt someone, so that seems okay.[paragraph break]Walking through the path you made, you see a high cup near the top of the pile.";
	move pike rust to Pile Up Isle;
	move base pikes to Pile Up Isle;
	moot bay spikes;

a wordtwisting rule (this is the pre-hike-up rule):
	if player is not in a pile up isle, unavailable;
	if sco-hike-up is true:
		vcal "There's nothing else to be found from another hike up. You're not getting the high cup, and you don't need it.";
		already-done;
	ready;

this is the post-hike-up rule:
	now sco-hike-up is true;
	say "You decide the direct way of climbing the pile at a steep angle just won't work. But it's a big pile, and as you walk around, you see a slower way, less steep, but less likely to cause you to slip. You get to the top and have a look around. From your vantage point, you see a new path from the Ur-Branch, to the southwest! You also see places well beyond it, to the west and south. Well, that should cover all the directions.[paragraph break]It makes you almost forget about the high cup. Well, it turns out the high cup is glued to other trash. Dang it! You're not sure if you needed it, anyway. It looks kind of fancy.";

book southeast branch sorting

chapter dome aching scoring

a wordtwisting rule (this is the pre-can-take rule):
	if player is not in dome aching, unavailable;
	if sco-can-take is true:
		vcal "You already affirmed you can take it.";
		already-done;
	ready;

this is the post-can-take rule:
	now sco-can-take is true;
	say "You feel you can take the aching. It takes a bit of time, and you worry a worse aching may replace it ... and, in a way, it does. You wonder what you need to do here now. You see great inns and an inner nest. Which is worth visiting?";
	move great inns to Dome Aching;
	move inscen to Dome Aching;

a wordtwisting rule (this is the pre-gray-tins rule):
	if player is not in dome aching, unavailable;
	if sco-gray-tins is true:
		vcal "You already got gray tins from the Great Inns!";
		already-done;
	ready;

this is the post-gray-tins rule:
	now sco-gray-tins is true;
	say "Why not? You can always use tins.";
	now player has tins;
	declue great inns;

a wordtwisting rule (this is the pre-dough-making rule):
	if player is not in dome aching, unavailable;
	if sco-can-take is false:
		vcp "The can't-ache prevents you from doing anything constructive!";
		not-yet;
	if player does not have gray tins:
		vcp "You have nowhere to put the dough yet.";
		not-yet;
	if sco-dough-making is true:
		vcal "You already made enough dough!";
		already-done;
	ready;

this is the post-dough-making rule:
	now sco-dough-making is true;
	say "Excellent! With the tin, you have somewhere to put the dough.";
	now player has dough;
	declue-here;

a wordtwisting rule (this is the pre-in-earnest rule):
	if player is not in dome aching, unavailable;
	if inscen is not in dome aching, unavailable;
	if sco-in-earnest is true:
		vcal "Don't want to get too earnest, here.";
		already-done;
	ready;

this is the post-in-earnest rule:
	now sco-in-earnest is true;
	say "The inner nest seems to open up a bit more, comfortable you're not just running through a checklist. Well, perhaps you still are. But worst case, you took the time to pretend and make it believable. You can now go [b]IN[r].";
	reveal inner nest to inside;
	declue inscen;

chapter inner nest scoring

a wordtwisting rule (this is the pre-pry-more rule):
	if player is not in inner nest, unavailable;
	if sco-pry-more is true:
		vcal "You already pried and got the prime oar!";
		already-done;
	ready;

this is the post-pry-more rule:
	now sco-pry-more is true;
	say "Surely there must be some way to get the oar, you think. And you pry constructively. After an hour's work, you have an oar, lightweight and powerful.";
	now player has prime oar;
	declue prime oar;

book southwest branch sorting

chapter fort earns four turns scoring

a wordtwisting rule (this is the pre-oh-clever rule):
	if location of player is not fort earns, unavailable;
	if sco-oh-clever is true:
		vcal "You already pulled the oak lever and made it disappear! You just need to do something with [the wick] from here on out.";
		already-done;
	ready;

this is the post-oh-clever rule:
	now sco-oh-clever is true;
	say "You think and think and think. Of course, they wouldn't want any old fool pulling the lever. So there's no obvious way to pull it. You try a hundred different ways, then feel clever on the hundred and first when -- CLICK -- it flips, revealing a compartment behind![paragraph break]The feeling of cleverness lasts only a few seconds, though. You realize how you could've seen it sooner if you'd been paying attention. After fifteen minutes, you feel even less clever for having spent fifteen minutes unconstructively visiting the past.[paragraph break]Then you notice what the lever was hiding: a be-quick beak wick! Nice if you need speed, you're sure.";
	move beak wick to Fort Earns Four Turns;
	moot oak lever;

a wordtwisting rule (this is the pre-fell-trap rule):
	if player is not in fort earns, unavailable;
	if sco-fell-trap is true:
		vcal "You can just X TRAP to [if cellar bin is visited]return[else]see what it might do[end if].";
		already-done;
	ready;

this is the post-fell-trap rule:
	now sco-fell-trap is true;
	say "Oh my! You pull the felt wrap away, and that is one fell looking trap!";
	moot felt wrap;
	move fell trap to Fort Earns;

chapter cell urban scoring

a wordtwisting rule (this is the pre-cellar-bin rule):
	if sco-cellar-bin is true and (player is in fort earns or player is in cellar bin):
		vcal "Whenever you want to revisit the cellar bin, you can just [b]X TRAP[r].";
		already-done;
	if player is not in cell urban, unavailable;
	ready;

this is the post-cellar-bin rule:
	now sco-cellar-bin is true;
	say "You bang on the cell door, claiming you've been sent to the wrong place. To your surprise, two maintenance workers come by and agree. One pushes a button. You are spun around! You fall to the floor, dizzy.[paragraph break]'Okay. Next time, if you fall in the trap, you'll fall somewhere else.' You thank them, you guess, not entirely sure what good it will do.";
	move player to Cellar Bin;

book west branch sorting

chapter Bowl Ditch scoring

a wordtwisting rule (this is the pre-bold-itch rule):
	if player is not in bowl ditch, unavailable;
	if sco-bold-itch is true:
		vcal "You already shook off your initial embarrassment and fears. Too much itch, and you'll get rash";
		already-done;
	ready;

this is the post-bold-itch rule:
	now sco-bold-itch is true;
	say "You have a think. It's not terrible. You've gotten here. You can get back. You really did trample through whatever was to the east. You walk off the ankle sprain you get from falling down. At the edges of the ditch, you notice:  hey, isn't that a clay mitt? And hey, you can see things clearing out to the north!";
	move clay mitt to Bowl Ditch;
	reveal Sob Acres to north;
	reveal Turbo Tours to west;
	declue-here;

a wordtwisting rule (this is the pre-claim-it rule):
	if player is not in bowl ditch and player does not have clay mitt, unavailable;
	if sco-claim-it is true:
		vcal "You reclaim the clay mitt, which makes you feel powerful and proud, I guess.";
		already-done;
	ready;

this is the post-claim-it rule:
	now sco-claim-it is true;
	say "You wonder if someone would really miss this clay mitt. But then you realize there's nothing stopping you, and they probably didn't just stop by here on vacation and lose it.";
	now player has clay mitt;
	declue clay mitt;

chapter sob acres scoring

a wordtwisting rule (this is the pre-saw-bakers rule):
	if player is not in sob acres, unavailable;
	if sco-saw-bakers is true:
		vcal "You already discovered the deli!";
		already-done;
	ready;

this is the post-saw-bakers rule:
	now sco-saw-bakers is true;
	say "You keep an eye out for other people. Eventually, you see them: bakers, upset they're getting no business. You can probably go INSIDE to their deli.";
	reveal Deli Stern to inside;
	declue-here;

chapter deli stern scoring

a wordtwisting rule (this is the pre-dell-eastern rule):
	if player is not in deli stern, unavailable;
	if sco-dell-eastern is true:
		vcal "You already made things nicer to the east!";
		already-done;
	ready;

this is the post-dell-eastern rule:
	now sco-dell-eastern is true;
	say "The surroundings are a bit more welcoming now. Oh, you can go east as well as out, too.";
	reveal Sob Acres to the east;
	declue-here;

chapter turbo tours scoring

a wordtwisting rule (this is the pre-stark-raft rule):
	if player is not in turbo tours, unavailable;
	if sco-stark-raft is true:
		vcal "You already build the stark raft!";
		already-done;
	ready;

this is the post-stark-raft rule:
	now sco-stark-raft is true;
	say "You let slip that perhaps a stark raft is more in your price range. A voice from behind the star craft agrees. They, in fact, have a particularly plain one which has been not at all good for business. They're willing to give it to you, if you promise not to loiter too much. Thus, they redirect you away to a new area of Turbo Tours, one without the star craft. You feel belittled, but hey, a free raft is a free raft. Your prime oar should work just fine on it.";
	move stark raft to Turbo Tours;
	moot star craft;

a wordtwisting rule (this is the pre-tour-boaters rule):
	if player is not in turbo tours, unavailable;
	if sco-tour-boaters is true:
		vcal "You already brought in additional business to Turbo Tours!";
		already-done;
	ready;

this is the post-tour-boaters rule:
	now sco-tour-boaters is true;
	say "You have a business idea! You explain it to the folks at Turbo Tours. They play it down, but of course, after a bit they implement it and it works well and they take credit for it.[paragraph break]That's sort of a bummer, but you find it nice to be around people who are having their own adventures.";

chapter far miles farm isles scoring

a wordtwisting rule (this is the pre-find-rinks rule):
	if player is not in far miles farm isles, unavailable;
	if sco-find-rinks is true:
		vcal "You already found them! [if sco-bear-respond is false]You don't need to go back[else]You just need to figure what to do to the south[end if].";
		already-done;
	ready;

this is the post-find-rinks rule:
	now sco-find-rinks is true;
	say "It's too warm out for ice rinks, but then, you didn't expect any clues to be obvious. Perhaps you just need to look for reasonably-sized ponds that could freeze pretty quickly in the winter. A bit of wandering, and you find them...";
	reveal Berries Pond to south;
	declue fine drinks;
	move player to Berries Pond;

a wordtwisting rule (this is the pre-barn-open rule):
	if player is not in far miles farm isles, unavailable;
	if sco-barn-open is true:
		vcal "You already got an egg from the barn!";
		already-done;
	ready;

this is the post-barn-open rule:
	now sco-barn-open is true;
	say "Hooray! You figured what to do! You get a point!";
	declue bar nopin;

book south branch sorting

chapter tube rod scoring

a wordtwisting rule (this is the pre-too-broad rule):
	if player is not in tube rod, unavailable;
	if sco-too-broad is true:
		vcal "You already burst the tube/rod!";
		already-done;
	ready;

this is the post-too-broad rule:
	now sco-too-broad is true;
	say "The tube/rod slowly cracks, then bursts. A passage now leads south. You pause a bit and hope it's not a tube leading to bleeding...";
	reveal Dam Pink to south;
	declue-here;

chapter dam pink scoring

a wordtwisting rule (this is the pre-damp-ink rule):
	if player is not in dam pink, unavailable;
	if sco-gas-pouch is false:
		vcp "But you have nothing that can hold the ink without leaking!";
		not-yet;
	if sco-damp-ink is true:
		vcal "You got enough ink.";
		already-done;
	ready;

this is the post-damp-ink rule:
	now sco-damp-ink is true;
	say "Hooray! You figured what to do! You get a point!";
	declue-here;

chapter too dark tude ark scoring

a wordtwisting rule (this is the pre-ho-langour rule):
	if player is not in tude ark, unavailable;
	if sco-too-broad is false:
		vcp "But it doesn't quite work yet, calling for languor ... you haven't done anything down this way.";
		not-yet;
	if sco-ho-langour is true:
		vcal "You already dealt with your anger! If you want more anger to have to deal with, why, you might get your wish, if you're not careful.";
		already-done;
	ready;

this is the post-ho-langour rule:
	now sco-ho-langour is true;
	say "Passive resistance is the way to go! You worry you are just doing nothing by default, and it doesn't really count, but then you remember how you busted up the tube/rod. You feel your blood pressure falling. Things aren't so bad, now.";
	reveal Scribe Room to south;
	declue whole anger;

chapter scribe room scoring

a wordtwisting rule (this is the pre-scry-broom rule):
	if player is not in scribe room, unavailable;
	if sco-damp-ink is false:
		vcp "The scribes won't trade a broom for NOTHING...";
		not-yet;
	if sco-scry-broom is true:
		vcal "You neither want nor need two brooms.";
		already-done;
	ready;

this is the post-scry-broom rule:
	now sco-scry-broom is true;
	say "You realize a broom must be here. You trade the damp ink in the gas pouch for it.";
	moot gas pouch;
	declue-here;

chapter sage oaks scoring

a wordtwisting rule (this is the pre-say-jokes rule):
	if player is not in sage oaks, unavailable;
	if sco-say-jokes is true:
		vcal "Don't push it. The sage oaks might find such foolish risks stupid and change their views of you.";
		already-done;
	ready;

this is the post-say-jokes rule:
	now sco-say-jokes is true;
	say "'Eh jokes age oaks,' they mutter at first. But they get into it more. They warn you of the trouble you'll get in if you keep telling corny jokes. It doesn't show you're smart. But then you explain you carefully crafted said jokes to peeve them. You've had experience all game with wordplay, too.[paragraph break]They're pretty impressed with your presentation. You feel a magic surge--you can pass to the south now.";
	reveal dust which does twitch to south;
	declue-here;
	declue sageoakscen;

a wordtwisting rule (this is the pre-gas-pouch rule):
	if player is not in sage oaks and player does not have gas pouch, unavailable;
	if sco-gas-pouch is true:
		vcal "You don't need two gas pouches.";
		already-done;
	ready;

this is the post-gas-pouch rule:
	now sco-gas-pouch is true;
	say "The sage oaks chuckle. And you chuckle a bit, too. Turnabout is fair play! They release the gas from the pouch all at once -- of course, not in your direction. 'It can hold liquid, too,' they say.[paragraph break]You step to the south.[paragraph break]'You are free to go that way,' they intone. But they offer no other clues.";

chapter dust which does twitch scoring

a wordtwisting rule (this is the pre-pro-sweeping rule):
	if player is not in dust which, unavailable;
	if sco-scry-broom is false:
		vcp "You have nothing that could sweep!";
		not-yet;
	if sco-pro-sweeping is true:
		vcal "It's clean enough here.";
		already-done;
	ready;

this is the post-pro-sweeping rule:
	now sco-pro-sweeping is true;
	say "The dust gets clearer. You see you are in a library.";
	moot prose weeping;

volume loose intro

table of intro jabber
my-jabber
"There's a brief explanation about how [this-game] reuses a lot of the code from [wp], though hopefully the different stories and puzzles give enough variety to keep you interested."

volume directions

table of noways
noway-rm	noway-txt
My New Mine Ooh	"You could explore your mine, but you [if sco-try-quest is false]have no purpose, or errand, or ... there's another word, here[else if sco-be-strong is false]really should get rid of that beast[else]sense the real adventure lies outside[end if]."
Ur Branch	"You can't go [noun][if noun is eventual] yet[end if][if south is branchcan and west is branchcan]. There are no new ways to go[end if]."
Beach Ill	"You can only walk back west[if sco-punt-weaker is true] or take the punt east[end if]."
Pile Up Isle	"You can't see land anywhere except to the west."
Sore Dark	"The only exit from this cavern is southeast."
Bore Dread	"The only exit from this enclosure is southwest."
Blah Copse	"You can only leave the copse to the north[if sco-or-clerk is true] or south[end if]."
Morph Lairs	"Since you can't dare the morph lairs, the only way to leave is back south."
Pea Stalks	"Compass directions befuddle you at the moment. You can only go back out."
Dome Aching	"The only compass direction you can go is back northwest. However, you may also [if sco-in-earnest is true]go[else]find a way[end if] in to the inner nest."
Inner Nest	"No secret passages here. You just need to go back out."
Fort Earns	"You can leave the fort to the northeast [if cellar bin is visited]or X TRAP to fall to the cellar bin[end if]."
Cell Urban	"You'll have to use your wits to leave."
Cellar Bin	"Just go up to leave."
Bowl Ditch	"You can only go [if sco-bold-itch is true]north or [end if]back east."
Sob Acres	"While it's pretty open here, [if sco-saw-bakers is true]the only safe ways are south or [acres-to-deli][else]you see safety only to the south, for now[end if]."
Deli Stern	"Not that way. Out[if sco-dell-eastern is true] or east[end if]."
Tube Rod	"Only back north. Well, for now."
Sage Oaks	"Only back north. Well, for now."
Too Dark Tude Ark	"Only back north. Well, for now."

to say acres-to-deli: say "[if sco-dell-eastern is true]west/[end if]in"

Us Too Tables ends here.

---- DOCUMENTATION ----
